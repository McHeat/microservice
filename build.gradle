// 声明gradle本身需要的资源，支持其它build脚本的运行-优先其他执行
buildscript {
    ext {
        springBootVersion = '2.0.8.RELEASE'
        springCloudVersion = 'Finchley.SR2'
    }
    repositories {
        mavenLocal()
        maven {
            url "http://maven.aliyun.com/nexus/content/repositories/central/"
        }
        mavenCentral()
    }
    dependencies {
        // 在执行其余的build脚本时，class loader可以使用的依赖项
        classpath("io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

group "com.mcheat.ms"
version "1.0.0"

// 项目需要的资源
subprojects {
    // 加入插件
    apply plugin: 'java'
    apply plugin: "idea"
    apply plugin: "maven"
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8
    ext {
        // project的额外属性，用于定义profile属性，模拟maven的profile
        if (project.hasProperty("profile")) {
            profile = project["profile"]
        } else {
            profile = "dev"
        }
    }


    repositories {
        mavenLocal()
        maven {
            url "http://maven.aliyun.com/nexus/content/repositories/central/"
        }
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    dependencies {

        compile "org.projectlombok:lombok"
    }

    configurations.all {
        // 默认情况下包在上传后24小时更新一次，配置修改为10s
        resolutionStrategy.cacheChangingModulesFor 10, 'seconds'
    }
}